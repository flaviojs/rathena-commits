//===== rAthena Script ======================================= 
//= Renewal Swordman Job Change
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Job Change to Swordman Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added Baby job change support. [Masao]
//============================================================ 
izlude_in,74,172,4	script	Swordman	119,{
	if (Upper == 1) {
		if ((Class == Job_Novice_High) && (ADVJOB == Job_Lord_Knight || ADVJOB == Job_Paladin)) {
			mes "[Swordman Guildsman]";
			mes "It...";
			mes "Can't be...";
			mes "You've been reborn, haven't you?";
			next;
			mes "[Swordman Guildsman]";
			mes "I see you're retreading the path of the Swordman! Once you've gotten used to brandishing a sword, you can never go back!!";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Swordman Guildsman]";
				mes "Hmm? Ah, you must first master the Basic Skills before you are ready to become a Swordman.";
				next;
				mes "[Swordman Guildsman]";
				mes "Come back to me when you have finished learning the Basic Novice Skills.";
				close;
			}
			mes "[Swordman Guildsman]";
			mes "Excellent! Let me promote you to a Swordman right away!";
			next;
			skill 143,0,1;
			jobchange Job_Swordman_High;
			skill 144,1,0;
			skill 145,1,0;
			skill 146,1,0;
			mes "[Swordman Guildsman]";
			mes "Hmm... You look like a well-experienced Swordman. Still, I'm sure that you must train to improve your skills and gain strength!";
			close;
		}
		mes "[Swordman Guildsman]";
		mes "Hm...?";
		mes "You're a reborn";
		mes "warrior, aren't you?";
		next;
		mes "[Swordman Guildsman]";
		mes "Hmmm...";
		mes "It seems that being";
		mes "a Swordman is not part";
		mes "of your destiny. I'm sorry,";
		mes "but it seems there is nothing";
		mes "I can do for you.";
		close;
	}
	mes "[Swordman Guildsman]";
	mes "Welcome to the";
	mes "Swordman Association!";
	next;
	mes "[Swordman Guildsman]";
	mes "So...";
	mes "What business";
	mes "brings you to us?";
	next;
	switch (select("Tell me about being a Swordman.:I want to be a Swordman.:Cancel.")) {
	case 1:
		mes "[Swordman Guildsman]";
		mes "So you wish to know more about the mighty Swordman job? Well, then...";
		next;
		mes "[Swordman Guildsman]";
		mes "Amongst the First Class jobs, the Swordman is the best melee fighter for three reasons.";
		mes "There are 3 reasons why Swordy is the best to approch a fight!";
		next;
		mes "[Swordman Guildsman]";
		mes "First, Swordman has the benefit of additional HP. Second, Swordman generally have access to a wider selection fo weapons than the other First Class jobs.";
		next;
		mes "[Swordman Guildsman]";
		mes "And third, most of the Swordman skills are crushing physical attacks! In my opinion, being a Swordman is the best job ever!";
		close;
	case 2:
		if (Class == Job_Baby || Class == Job_Novice) {
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Swordman Guildsman]";
				mes "I'm sorry to tell you this but to be a Swordman, you must reach at least ^4d4dffJob Level 10^000000.";
				mes "and ^4d4dffBasic Skill Level 9^000000.";
				next;
				mes "[Swordman Guildsman]";
				mes "Want to be a Swordman without having the minimum requirement?";
				mes "Do you think being a Swordman is that easy?";
				close;
			}
			mes "[Swordman Guildsman]";
			mes "Hmm, both your Job Level and Basic Skill Level check out.";
			mes "Good. Do you want to be a Swordman right away?";
			next;
			switch (select("Yes, I do.:I'll consider it again.")) {
			case 1:
				mes "[Swordman Guildsman]";
				mes "Congratulations! From now on, you are going to live a Swordman's life!";
				mes "I will transform you right away!";
				next;
				skill 143,0,1;
				if(Class == Job_Baby){
					jobchange Job_Baby_Swordman;
				} else {
					jobchange Job_Swordman;
				}
				getitem 13415,1; //N_Falchion
				mes "[Swordman Guildsman]";
				mes "Congratulations again for becomming a Swordman and I hope that you participate in many activities for the revival of our guild.";
				close;
			case 2:
				mes "[Swordman Guildsman]";
				mes "Yeah. Careful consideration is needed for choosing a job.";
				mes "But I feel sorry... that you'll have to consider it again after all the trials....";
				close;
			}
		}
		if (Class == Job_Baby_Swordman || Class == Job_Swordman) {
			mes "[Swordman Guildsman]";
			mes "You are already an excellent Swordman, aren't you?";
			mes "Just devote yourself to be a great Swordman.";
			close;
		}
		mes "[Swordman Guildsman]";
		mes "Haha! Oh boy. I'm flattered, but you already have another job! Still, I can't blame you...";
		close;
	case 3:
		mes "[Swordman Guildsman]";
		mes "Ha ha ha!";
		mes "Ah, youth!";
		close;
	}
}