//===== rAthena Script ======================================= 
//= Renewal Merchant Job Change
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Job Change to Merchant Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added Baby job change support. [Masao]
//============================================================ 
alberta_in,53,43,6	script	Merchant#mer	86,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Whitesmith || ADVJOB == Job_Creator)) {
			mes "[Chief Mahnsoo]";
			mes "Long time no see!";
			mes "Hey, you didn't quit";
			mes "your business, did you?";
			mes "What happened?";
			next;
			mes "[Chief Mahnsoo]";
			mes "Whoa...";
			mes "You've actually been to Valhalla?! Wow, you've come a long way...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Chief Mahnsoo]";
				mes "Hmmm...";
				mes "It seems that you're not ready to become a Merchant again. Go finish learning the Basic Novice Skills first.";
				next;
				mes "[Chief Mahnsoo]";
				mes "Don't worry, we'll always have a Merchant position open for you. Just come back when you're ready, okay?";
				close;
			}
			mes "[Chief Mahnsoo]";
			mes "I guess it's destiny that we meet like this once more. Alright. Once again, let me change you into a Merchant!";
			next;
			skill 143,0,1;
			jobchange Job_Merchant_High;
			skill 153,1,0;
			skill 154,1,0;
			skill 155,1,0;
			mes "[Chief Mahnsoo]";
			mes "Ah~ How nostalgic. Just like old times! Alright, do your best!";
			close;
		}
		mes "[Chief Mahnsoo]";
		mes "^333333*Sigh*^000000";
		mes "I'm so bored...";
		mes "When will I hear from my lovely Blossom?";
		close;
	}
	mes "[Chief Mahnsoo]";
	mes "So, what brings you to";
	mes "the Merchant Association?";
	mes "Is there anything";
	mes "I can help you with?";
	next;
	switch (select("I want to be a Merchant.:Tell me about Merchants.:Tell me the requirements.:Nope.")) {
	case 1:
		if (Class == Job_Baby || Class == Job_Novice) {
			mes "[Guildsman Mahnsoo]";
			mes "Want to be a merchant? Hmm...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Guildsman Mahnsoo]";
				mes "But if you want to be a merchant, your basic skill level must reach Level 9 or you must spend all of your skill points.";
				next;
				mes "[Guildsman Mahnsoo]";
				mes "Don't you think we need to learn some basic skills although we just deal with money?";
				close2;
			}
			mes "[Guildsman Mahnsoo]";
			mes "Good, I think you're fully ready for it seeing that your basic skill level is fulfilled.";
			mes "Now I allow you to be a merchant.";
			next;
			mes "[Guildsman Mahnsoo]";
			mes "Congratulations on becoming a merchant!";
			next;
			skill 143,0,1;
			if(Class == Job_Baby){
				jobchange Job_Baby_Merchant;
			} else {
				jobchange Job_Merchant;
			}
			set job_merchant_q,0;
			set job_merchant_q2,0;
			getitem 1381,1; //N_Battle_Axe
			mes "[Guildsman Mahnsoo]";
			mes "Congratulations again for being a member of the merchant guild and one of our colleagues. I expect your active participation from now on!";
			next;
			mes "[Guildsman Mahnsoo]";
			mes "Absolutely, we need young people who have passion to achieve our great goal for securing 20% of the worldwide currency volume. You get it? Huh? ";
			mes "Well, I'm just saying... It means let's make lots of money in the end. You guys know that~";
			close;
		}
		if (Class == Job_Baby_Merchant || Class == Job_Merchant || Class == Job_Merchant_High) {
			mes "[Chief Mahnsoo]";
			mes "Hello there!";
			mes "How do you like";
			mes "being a Merchant?";
			next;
			mes "[Chief Mahnsoo]";
			mes "Having a way with";
			mes "money certainly";
			mes "has its perks,";
			mes "does it not?";
			close;
		}
		mes "[Chief Mahnsoo]";
		mes "We Merchants hate people who are two faced. It's bad for business.";
		next;
		mes "[Chief Mahnsoo]";
		mes "People who always try to take advantage of other people by selling things at a ridiculous price just so they can make money that they'll waste are the worst.";
		next;
		mes "[Chief Mahnsoo]";
		mes "Well, in any case, we only accept Novices for job changes to the Merchant class. But I appreciate your interest in what we do.";
		close;
	case 2:
		mes "[Chief Mahnsoo]";
		mes "Merchant?";
		mes "Well, we basically sell goods to make money. That is the way";
		mes "of the Merchant.";
		next;
		mes "[Chief Mahnsoo]";
		mes "I guess we may not be the best at fighting, and we don't have many special attacks. We've got no healing skills...";
		next;
		mes "[Chief Mahnsoo]";
		mes "But we can buy goods at lower prices from NPC shops and sell them at a higher price to other people~";
		next;
		mes "[Chief Mahnsoo]";
		mes "Our ultimate attack skill is 'Mammonite.' The strength of Mammonite comes from the anger";
		mes "when we're forced to throw away perfectly good zeny.";
		next;
		mes "[Chief Mahnsoo]";
		mes "Throwing away zeny like that";
		mes "causes a deadly rage to well up in the heart of any Merchant!";
		mes "Just thinking about it";
		mes "makes my blood boil!";
		next;
		mes "[Chief Mahnsoo]";
		mes "Anyway, we can use most";
		mes "weapons except Bows, Rods, and Two-Handed Swords. But we can always sell those.";
		next;
		mes "[Chief Mahnsoo]";
		mes "Yes...";
		mes "We Merchants generally";
		mes "have money on our minds...";
		close;
	case 3:
		mes "[Guildsman Mahnsoo]";
		mes "To become a Merchant,";
		mes "although just selling and receiving money is our job,";
		mes "you will need to learn all Basic Skills.";
		next;
		mes "[Guildsman Mahnsoo]";
		mes "Well, we used to receive a Membership Fee. But was it so hard to make that money?";
		mes "There were too many people who couldn't gather that money and kept crying.";
		next;
		mes "[Guildsman Mahnsoo]";
		mes "So recently, we decided to allow any Novice who wants to be a Merchant become one.";
		mes "Because staying alive or not, it all rests on your own ability.";
		close;
	case 4:
		close;
	}
}

alberta_in,28,29,2	script	Merchant Guildsman	83,{
	if (job_merchant_q == 0 || job_merchant_q == 1 || job_merchant_q == 2) {
		mes "[Union Staff Kay]";
		mes "I am sorry, but we don't make deliveries anymore.";
		mes "Didn't Chief Mahnsoo tell you?";
		close;
	}
	mes "[Union Staff Kay]";
	mes "Ah, we don't make deliveries anymore.";
	mes "Too many people cried and kept whining that it's too difficult.";
	next;
	mes "[Union Staff Kay]";
	mes "Well, maybe we will have a few deliveries later.";
	mes "Anyway, I don't have any work now...";
	close;
}

morocc_in,140,102,4	script	Student#mer	86,{
	if (job_merchant_q == 4 || job_merchant_q == 3) {
		mes "[Dyer's Student]";
		mes "I am sorry, but the Merchant Guild no longer makes deliveries to us anymore.";
		close;
	}
	mes "[Dyer's Student]";
	mes "Mr. Java Dullihan is the one and only, the best dye maker on the Rune-Midgard continent.";
	next;
	mes "[Dyer's Student]";
	mes "Aaaand I'm proud to say that I'm his student! Someday, I'll be able to make really beautiful dyes too!";
	next;
	mes "[Dyer's Student]";
	mes "Of course, I'm still learning the basics right now, but someday...";
	close;
}

geffen_in,155,122,4	script	Guild Staff	47,{
	if (job_merchant_q == 4 || job_merchant_q == 3) {
		mes "[Guild Staff]";
		mes "I am sorry, but the Merchant Guild no longer makes deliveries to us anymore.";
		close;
	}
	mes "[Guild Staff]";
	mes "My package should have arrived by now. Huh. I guess the Merchant Guild might be running a little late...";
	close;
}