//===== rAthena Script ======================================= 
//= Renewal Acolyte Job Change
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Job Change to Acolyte Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added back the npcs used for the priest quest.
//= 1.2 Added Baby job change support. [Masao]
//============================================================ 
prt_church,184,41,4	script	Cleric#aco	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "[Father Mareusis]";
				mes "Ah, I sense you have endured";
				mes "a past life experience. You must have learned many things before entering Valhalla.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Father Mareusis]";
					mes "Unfortunately, I don't think you're ready to become an Acolyte yet. Please finish learning all of the Basic Skills first.";
					next;
					mes "[Father Mareusis]";
					mes "In the meantime,";
					mes "I will wait until";
					mes "you are ready.";
					mes "May God be";
					mes "with you.";
					close;
				}
				mes "[Father Mareusis]";
				mes "Well, I welcome you";
				mes "back from Valhalla and";
				mes "wish you luck on your";
				mes "new life's journey.";
				next;
				skill 143,0,1;
				jobchange Job_Acolyte_High;
				skill 156,1,0;
				mes "[Father Mareusis]";
				mes "Now, venture forth and seek those who need your help. May God light your path.";
				close;
			}
			mes "[Father Mareusis]";
			mes "Now, venture forth to seek people who need your help. May God enlighten your way.";
			close;
		}
		mes "[Father Mareusis]";
		mes "I sense that you have endured a past life experience. You must have learned many things before entering Valhalla.";
		next;
		mes "[Father Mareusis]";
		mes "However, I can tell that you are not suited to be an Acolyte. Please remember who you were in your past life and find your path.";
		close;
	}
	mes "[Father Mareusis]";
	mes "What is it that you seek?";
	next;
	switch (select("Father, I want to be a Acolyte.:Acolyte Requirements.:Just looking around.")) {
	case 1:
		if (Class == Job_Baby_Acolyte || Class == Job_Acolyte) {
			mes "[Father Mareusis]";
			mes "Are you feeling okay today? I can tell by your attire that you are already an Acolyte. You're not joking around, are you?";
			close;
		}
		if ((Class != Job_Baby) && (Class != Job_Novice)) {
			mes "[Father Mareusis]";
			mes "I'm sorry, but we can only accept Novices as applicants for the job change to Acolyte.";
			close;
		}
		mes "[Father Mareusis]";
		mes "Do you truly wish to become a servant of God?";
		mes "Let's see whether you are ready for it or not... Hmm...";
		next;
		if (getskilllv("NV_BASIC") < 9) {
			mes "[Father Mareusis]";
			mes "Good Lord! Haven't you accomplished the Basic Training yet?! It's important that you finish that!";
			next;
			mes "[Father Mareusis]";
			mes "You should have trained more! Go back and make sure you reach Novice Job Level 9 and learn all of the Basic Skills!";
			close;
		}
		mes "[Father Mareusis]";
		mes "Hmm... your job level is high enough and you've proven your qualification.";
		next;
		mes "[Father Mareusis]";
		mes "I am proud to say that you are now ready to become an Acolyte!";
		next;
		skill 143,0,1;
		if(Class == Job_Baby){
			jobchange Job_Baby_Acolyte;
		} else {
			jobchange Job_Acolyte;
		}
		set ACO_Q,0;
		getitem 1545,1; //N_Mace
		mes "[Father Mareusis]";
		mes "Always remember to be thankful to God, who is taking care of us all the time.";
		next;
		mes "[Father Mareusis]";
		mes "Always use your gifts to serve Him by helping others. In chaos and times of difficulty, face your hardships with unwavering faith.";
		next;
		mes "[Father Mareusis]";
		mes "Lastly, I want to sincerely congratulate you on persevering through your trial of penance.";
		close;
	case 2:
		mes "[Father Mareusis]";
		mes "Do you wish to become an Acolyte?";
		mes "Then, you must fulfill the following requirements thinking those are the practices given by God.";
		next;
		mes "[Father Mareusis]";
		mes "First, you have to reach at least Novice Job Level 9 and learn all of the Basic Skills.";
		mes "This is the most basic thing to do, so you need to regard it as the way of training yourself.";
		next;
		mes "[Father Mareusis]";
		mes "When you think you fulfilled this requirement I've asked of you. As long as your desire to serve God and others is sincere, you will be able to make it.";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	Ascetic#aco	89,{
	if (Class == Job_Baby || Class == Job_Novice) {
		if (ACO_Q == 6) {
			mes "[Father Rubalkabara]";
			mes "Please take care. They should know that you've met me by the time you arrive at the Prontera Sanctuary.";
			next;
			mes "[Father Rubalkabara]";
			mes "I've sent a carrier pigeon with a message. I hope it will arrive there safely...";
			close;
		}
		if (ACO_Q != 0) {
			if (ACO_Q == 2) {
				mes "[Father Rubalkabara]";
				mes "Oh...? You must be the one who aspires to become an Acolyte. I've already received news from the Sanctuary that you might be coming.";
				next;
				mes "[Father Rubalkabara]";
				mes "Now, your name was " + strcharinfo(0) + ", right? Excellent, thank you for visiting me.";
				next;
				mes "[Father Rubalkabara]";
				mes "I believe you've been told much about Acolytes from Friar Mareusis. Plus, there's plenty of helpful people in the Prontera Sanctuary.";
				next;
				mes "[Father Rubalkabara]";
				mes "I guess there's really no need for me to teach you much. Besides, I'm sure your someone from your generation may have trouble listening to an old man like me. Hahaha~";
				next;
				mes "[Father Rubalkabara]";
				mes "Still, lessons may come from the places you'd least expect. God loves to teach his children in strange ways. You'll see.";
				next;
				mes "[Father Rubalkabara]";
				mes "Well, I'll send the message telling them that you've come to visit me. So, you may now return to the Prontera Sanctuary.";
				next;
				mes "[Father Rubalkabara]";
				mes "Farewell.";
				savepoint "prt_fild03",361,255;
				set ACO_Q,6;
				close;
			}
			mes "[Father Rubalkabara]";
			mes "Oh...";
			mes "Are you one of the";
			mes "Acolyte applicants...?";
			mes "Let's see...";
			next;
			mes "[Father Rubalkabara]";
			mes "Your name is " + strcharinfo(0) + "?";
			mes "I don't think your name";
			mes "is on my list. Hmmm...";
			next;
			mes "[Father Rubalkabara]";
			mes "Why don't you go back to the Prontera Sanctuary and check again?";
			close;
		}
		mes "[Father Rubalkabara]";
		mes "Huh? What brings you here? This is a very dangerous place for a Novice like yourself!";
		close;
	}
	if (Class == Job_Baby_Acolyte || Class == Job_Acolyte) {
		if (ACO_Q2 != 0) {
			if (ACO_Q2 == 1) {
				mes "[Father Rubalkabara]";
				mes "Ah yes, so you're the young Acolyte who wishes to become a Priest.";
				next;
				mes "[Father Rubalkabara]";
				mes "I've received the message from Bishop Paul. Since you made the pilgrimage, I hope you accomplish your goal of becoming a Priest.";
				next;
				mes "[Father Rubalkabara]";
				mes "Also, if you have a chance, please visit this abbey again. It's a sacred place for our Church.";
				next;
				mes "[Father Rubalkabara]";
				mes "Well then, please head to your next destination for your pilgrimage. Be safe in your travels.";
				savepoint "prt_fild03",361,255;
				set ACO_Q2,2;
				close;
			}
			if (ACO_Q2 == 2) {
				mes "[Father Rubalkabara]";
				mes "Please be hurry to the next destination for your pilgrimage. I hope you become a Priest soon.";
				close;
			}
			mes "[Father Rubalkabara]";
			mes "May I ask why you have returned? Please go back and continue your religious practice.";
			close;
		}
		mes "[Father Rubalkabara]";
		mes "I have no idea what brought you here, but please excuse me.";
		close;
	}
	if (Class == Job_Baby_Priest || Class == Job_Priest) {
		mes "[Father Rubalkabara]";
		mes "Greetings.";
		next;
		mes "[Father Rubalkabara]";
		mes "Welcome to the Deep. Feel free to sit and contemplate God's message with me. This place is beautiful, even if danger accompanies its sense of serenity...";
		close;
	}
	mes "[Father Rubalkabara]";
	mes "Oh ho...";
	mes "Have you come into the Deep here for training? Or are you just a Wanderer?";
	next;
	mes "[Father Rubalkabara]";
	mes "Whoever you are, please take care of yourself. The monsters in here are shockingly strong, contrary to their cute appearance.";
	close;
}

moc_fild07,41,355,4	script	Ascetic#2	95,{
	if (Class == Job_Baby || Class == Job_Novice) {
		if (ACO_Q == 6) {
			mes "[Mother Marthilda]";
			mes "I will send a carrier pigeon to the Prontera Sanctuary. When you return, the Priest there should already have received my message.";
			next;
			mes "[Mother Marthilda]";
			mes "I will pray to God, and hope that you become an Acolyte soon.";
			close;
		}
		if (ACO_Q != 0) {
			if (ACO_Q == 3) {
				mes "[Mother Marthilda]";
				mes "Ah, you must be one of the Acolyte applicants. I sincerely welcome you.";
				next;
				mes "[Mother Marthilda]";
				mes "What is your name? " + strcharinfo(0) + "? Let's see... Ah, you're on my list.";
				next;
				mes "[Mother Marthilda]";
				mes "I will send a message to the Sanctuary confirming that you, " + strcharinfo(0) + " visited me and completed your penance.";
				next;
				mes "[Mother Marthilda]";
				mes "Please return to the Prontera Sanctuary and speak to the Priest in charge.";
				savepoint "moc_fild07",35,355;
				set ACO_Q,7;
				close;
			}
			mes "[Mother Marthilda]";
			mes "Ah...!";
			mes "You must be one";
			mes "of the Acolyte applicants.";
			mes "I sincerely welcome you.";
			next;
			mes "[Mother Marthilda]";
			mes "Now, what is your name?";
			mes "" + strcharinfo(0) + "? Let's see...";
			next;
			mes "[Mother Marthilda]";
			mes "Hmm...";
			mes "It seems your name";
			mes "is not on my list...";
			next;
			mes "[Mother Marthilda]";
			mes "Perhaps you should return to the Prontera Sanctuary and check the destination for your penance trial once again.";
			close;
		}
		mes "[Mother Marthilda]";
		mes "...";
		close;
	}
	if (Class == Job_Baby_Acolyte || Class == Job_Acolyte) {
		if (ACO_Q2 != 0) {
			if (ACO_Q2 == 1) {
				mes "[Mother Marthilda]";
				mes "Hmm...";
				mes "It seems you're";
				mes "training to become";
				mes "a Priest.";
				next;
				mes "[Mother Marthilda]";
				mes "However, at this point in your pilgrimage, I am not the person that you should be visiting.";
				next;
				mes "[Mother Marthilda]";
				mes "Perhaps you should ask Bishop Paul or Sister Cecilia once again. Well, may God be with you...";
				close;
			}
			if (ACO_Q2 == 2) {
				mes "[Mother Marthilda]";
				mes "Ah, are you";
				mes "a Priest trainee...?";
				mes "Welcome!";
				next;
				mes "[Mother Marthilda]";
				mes "We Priests are obliged to spread the message of God to";
				mes "the peoples of the Earth.";
				next;
				mes "[Mother Marthilda]";
				mes "After you become a Priest, your travels may take you to the Morroc Pyramids. I hope that you may release the Undead there from the evil to which they are bound.";
				next;
				mes "[Mother Marthilda]";
				mes "Well then...";
				mes "I shall pray to";
				mes "God for safety";
				mes "on your journey.";
				savepoint "moc_fild07",35,355;
				set ACO_Q2,3;
				close;
			}
			if (ACO_Q2 == 3) {
				mes "[Mother Marthilda]";
				mes "Please leave soon, and";
				mes "continue your training.";
				close;
			}
			mes "[Mother Marthilda]";
			mes "May I ask you the reason you came back? Please continue your training.";
			close;
		}
		mes "[Mother Marthilda]";
		mes "May God";
		mes "be with you...";
		close;
	}
	if (Class == Job_Baby_Priest || Class == Job_Priest) {
		mes "[Mother Marthilda]";
		mes "Hello there~";
		next;
		mes "[Mother Marthilda]";
		mes "How is your practice coming along? I certainly hope you're enjoying living in the grace of God.";
		close;
	}
	mes "[Mother Marthilda]";
	mes "May God";
	mes "be with you...";
	close;
}

prt_fild00,208,218,6	script	Ascetic#3	98,{
	if (Class == Job_Baby || Class == Job_Novice) {
		if (ACO_Q == 8) {
			mes "[Father Yosuke]";
			mes "What?";
			next;
			mes "[Father Yosuke]";
			mes "Have you any more business with me?! You don't! Go back to the Sanctuary now!";
			close;
		}
		if (ACO_Q != 0) {
			if (ACO_Q == 4) {
				mes "[Father Yosuke]";
				mes "Hey.";
				mes "Whatever you are,";
				mes "you look like an";
				mes "Acolyte applicant.";
				mes "Right?";
				next;
				mes "[Father Yosuke]";
				mes "Not bad, not bad. You withstood the penance trial pretty well.";
				mes "So what's your name?";
				next;
				mes "[Father Yosuke]";
				mes "" + strcharinfo(0) + ", huh?";
				next;
				mes "[Father Yosuke]";
				mes "Okay. I'll send a message to the Sanctuary that you, " + strcharinfo(0) + ", came to visit me.";
				next;
				mes "[Father Yosuke]";
				mes "Now go back to the Santuary and finish becoming an Acolyte, kid.";
				savepoint "prt_fild00",206,230;
				set ACO_Q,8;
				close;
			}
			mes "[Father Yosuke]";
			mes "Hey.";
			mes "You look like an Acolyte Applicant. Am I right?";
			next;
			mes "[Father Yosuke]";
			mes "Not bad at all, you've made it all the way here from Prontera. So what's your name, kid?";
			next;
			mes "[Father Yosuke]";
			mes "" + strcharinfo(0) + ", huh? Why isn't your name on my list?";
			next;
			mes "[Father Yosuke]";
			mes "You probably made a mistake. Go back to the Santuary, and check with the Bishop.";
			close;
		}
		mes "[Father Yosuke]";
		mes "You...";
		mes "Novice.";
		mes "There something";
		mes "you wanna tell me?";
		close;
	}
	if (Class == Job_Baby_Acolyte || Class == Job_Acolyte) {
		if (ACO_Q2 != 0) {
			if (ACO_Q2 == 1) {
				mes "[Father Yosuke]";
				mes "Hmm...";
				mes "A Priest trainee, eh?";
				next;
				mes "[Father Yosuke]";
				mes "Well, you got the wrong person. If you're supposed to meet up with me at all, it'd be later.";
				next;
				mes "[Father Yosuke]";
				mes "But that's your own fault.";
				mes "Now, go back to Church, kid.";
				close;
			}
			if (ACO_Q2 == 2) {
				mes "[Father Yosuke]";
				mes "Hmm...";
				mes "A Priest trainee, eh?";
				next;
				mes "[Father Yosuke]";
				mes "Well, you got the wrong person. If you're supposed to meet up with me at all, it'd be later.";
				next;
				mes "[Father Yosuke]";
				mes "But that's your own fault. Go back to Church.";
				close;
			}
			if (ACO_Q2 == 3) {
				mes "[Father Yosuke]";
				mes "Hmm.";
				mes "A Priest";
				mes "trainee, eh? ";
				mes "Welcome.";
				next;
				mes "[Father Yosuke]";
				mes "I won't say";
				mes "anything more.";
				mes "Just devote your";
				mes "life to God.";
				next;
				mes "[Father Yosuke]";
				mes "Now go back to church.";
				mes "Hereby, the first of";
				mes "your trials is now";
				mes "completed.";
				savepoint "prt_fild00",206,230;
				set ACO_Q2,4;
				close;
			}
			if (ACO_Q2 == 4) {
				mes "[Father Yosuke]";
				mes "I told you to go back to church.";
				mes "Or do you want to live with me here for the rest of your life...?";
				close;
			}
			mes "[Father Yosuke]";
			mes "Just go be a Priest. This isn't a playground for kids.";
			close;
		}
		mes "[Father Yosuke]";
		mes "...Acolyte, you don't have any business with me here.";
		close;
	}
	if (Class == Job_Baby_Priest || Class == Job_Priest) {
		mes "[Father Yosuke]";
		mes "Hey...";
		next;
		mes "[Father Yosuke]";
		mes "If you like, come sit here with me and meditate the great truths. God's majesty is truly inspiring...";
		close;
	}
	mes "[Father Yosuke]";
	mes "Do you have anything to say? Because unfortunately for you,";
	mes "I don't any replies.";
	close;
}